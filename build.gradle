plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
}

tasks {
    bootJar {
        enabled = false // Spring Boot JAR 비활성화
    }

    jar {
        enabled = true
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    group = 'com.mindshare'
    version = '1.0.0'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        if (project.name != 'mindshare-core') {
            implementation project(':mindshare-core') // core 모듈 가져오기
        }

        if (project.name != 'mindshare-core' && project.name != 'mindshare-cmm') {
            implementation project(':mindshare-cmm') // cmm 모듈 가져오기
        }

        // spring
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        // modelmapper
        implementation 'org.modelmapper:modelmapper:3.1.0'

        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // db
        runtimeOnly 'com.h2database:h2'

        // query dsl
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

        // annotation
        implementation 'com.google.code.findbugs:jsr305:3.0.2'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}
